\section{Constructing  the Abstract Syntax}
\label{04_01}

The first step in writing the program was to construct the abstract syntax. The abstract syntax determines how the parts of the "language" (linear logic in this case) are put together to form sentences. It is important to make sure the abstract syntax is well constructed, or strange things may happen in the program.

In this section, the different parts of the abstract syntax will explained individually, but the full abstract implementation can be found in appendix \ref{A_01} if the reader wants to look at it.

The fact that we split the connectives of linear logic up in section \ref{03_01_02} will help immensely. The resulting types can be translated almost directly into an abstract tree, which means we have a basic syntax already.

\lstCode{The first abstract syntax.}{04_01_C01}
\begin{lstgf}
abstract Laws = {
    
    flags startcat = Logic ;

    cat
        Logic ; Neg ; Pos ; Lolli ; Bang ; Atomic ; Conj ; ArgType ; Argument ArgType ;

    fun
        Formular : Neg -> Logic ;

        -- Positive types
        _Atom : Atomic -> Pos ;                       -- Turning an atomic into a positive type
        _Bang : Bang -> Atomic -> Pos ;               -- Using the unrestricted modality
        _Conj : Pos -> Conj -> Pos -> Pos ;           -- Using the simultaneous conjunction
        _Unit : Neg -> Pos ;                          -- Turns a negative into a positve
        _MPos : Pos -> Pos -> Pos ;                   -- Attaches multiple positives to each other

        -- Negative types
        _Pi : (A : ArgType) -> (Argument A -> Neg) -> Neg ; -- The universal quantification
        _Lolli : Pos -> Lolli -> Neg -> Neg ;         -- Using the linear implication
        _Mon : Pos -> Neg ;                           -- Turning a positive into a negative

        -- Connectives
        _Conj2 : Conj ;                               -- Simultaneous conjunction
        _Lolli2 : Lolli ;                             -- Linear implication
        _Bang2 : Bang ;                               -- Unrestricted modality

        -- Argument types
        _Nat, _Candidate, _List : ArgType ;
}
\end{lstgf}

Going through the abstract syntax, one will notice a couple of things that do not come from the connectives: There is something called an "\cf{Atomic}" (line 12) and there is both an \cf{ArgType} and an \cf{Argument A} in the function of \cf{\_Pi}. The \cf{Atomic} is used to represent functions along with the arguments they take. They will be explained in more detail later in this section.

The \cf{\_Pi} should seem familiar, as it was used for demonstrating variable bindings in section \ref{03_02_04}. The only things that have changed are the types involved. \cf{ArgType} is the type of the argument (a natural number, a candidate or a list), and \cf{Argument A} uses this \cf{ArgType} to bind a variable for use in the \cf{Neg}. This will allow the use of variables in the program.

There are two other things that needs an explanation as well. The \cf{\_Unit} turns a negative type into a positive type (it is actually a positive type, so there is no cheating here). The \cf{\_MPos} allows for multiple positive types to follow each other without connectives and is mainly used for allowing multiple universan quantifiers to follow each other.

The abstract syntax in \refCode{04_01_C01} introduced the \cf{Atomic}. The \cf{Atomic} needs an explanation and it needs to be defined as a function before the rest of the syntax can be written. An \cf{Atomic} represents a function in linear logic. This could be the \cf{voting-auth-card} or the \cf{blank-ballot} mentioned in section \ref{03_01_01}. An \cf{Atomic} can also represent a function that takes parameters, such as the ones in the example in section \ref{02_01}.

Looking at the example in section \ref{02_01}, one will notice that the formula not only contains functions, but that it also contains arithmetic and inequality operations (for example  $\bang$(N + 1 $<$ Q)). This is in place of a function, so the \cf{Atomic} needs to be able to represent that as well.

Putting that together, it means we need two kinds of atomics, one for functions and one for mathematical operations. The \cf{Atomic} is therefore represented in the following way in the abstract syntax.

\lstCode{Defining the \cf{Atomic} in the abstract syntax.}{04_01_C02}
\begin{lstgf}
    cat
        Atomic ; Ident ; MathFormula ;

    fun
        -- Atomic
        Atom_Ident : Ident -> Atomic ;          -- Represents the atomic variables/functions
        Atom_Math : MathFormula -> Atomic ;     -- Represents the mathematical operations
\end{lstgf}

With \cf{Atomic} defined, we have introduced two new categories at the same time; \cf{Ident} and \cf{MathFormula}. The \cf{Ident} represents the variables through the function name and the arguments it takes. To be able to use it, we need to define not just the \cf{Ident}, but also the arguments needed. The abstract syntax is once again extended.

\lstCode{Defining the \cf{Ident} and the arguments it needs.}{04_01_C03}
\begin{lstgf}
    cat
        Ident ; Arg ;

    fun
        -- Identifiers
        Ident_Uncounted : Arg -> Arg -> Ident ;
        Ident_Counted: Arg -> Arg -> Ident ;
        Ident_Hopeful : Arg -> Arg -> Ident ;
        Ident_Defeated : Arg -> Ident ;
        Ident_Elected : Arg -> Ident ;
        Ident_Quota : Arg -> Ident ;
        Ident_Winners : Arg -> Ident ;
        Ident_Begin : Arg -> Arg -> Arg -> Ident ;
        Ident_Count: Arg -> Arg -> Arg -> Ident ;
        Ident_BangElectAll : Ident ;
        Ident_BangDefeatAll : Ident ;
        Ident_DefeatMin : Arg -> Arg -> Arg -> Ident ;
        Ident_DefeatMin' : Arg -> Arg -> Arg -> Ident ;
        Ident_Minimum : Arg -> Arg -> Ident ;
        Ident_Transfer : Arg -> Arg -> Arg -> Arg -> Arg -> Ident ;
        Ident_Run : Arg -> Arg -> Arg -> Ident ;
        Ident_UnitOne : Ident ;

        -- Arguments
        _Arg : (A : ArgType) -> (a : Argument A) -> Arg ;
        _ArgNil, _ArgZ, _Arg1 : Arg ;
        _ArgMinus, _ArgPlus : Arg -> Arg ;
        _ArgList : Arg -> Arg -> Arg ;
        _ArgEmptyList : Arg ;
\end{lstgf}

Each \cf{Ident} uses the \cf{Arg}, so that will be explained first. \cf{Arg} represents any argument used in the formulas. In the example in section \ref{03_01_03}, \textit{hopeful C N} has the arguments \textit{C} and \textit{N}. They are the arguments bound through \cf{\_Pi}. To turn the variable binding into something that is easier to work with, \cf{\_Arg} is used. It takes two arguments and converts them to an \cf{Arg}, which could be the same as the \textit{C} from \textit{hopeful C N}. 

In addition to \cf{\_Arg}, there are three predefined arguments: \cf{\_ArgNil}, \cf{\_ArgZ} and \cf{\_Arg1}. Each of them represents an argument that has the same meaning no matter what logical formula it is used in. \cf{\_ArgNil} is the value "nil", \cf{\_ArgZ} is the value "z" or "zero" and \cf{\_Arg1} is the value 1. The last \cf{Arg}s are used to represent Celf's plus, minus and lists, and should require no deep explanation. 

Having understood the arguments, the identifiers are a bit simpler. Each variable used for the logical formulas concering laws (Note: Appendix with them?) is represented by an \cf{Identifier}. Each \cf{Identifier} takes between zero and five \cf{Arg}s, that are used to construct the Ident.

With the \cf{Ident}s in place (and thereby half the \cf{Atomic}s covered),  it is time to look at the other \cf{Atomic}: The one concerning mathematical formulas. The mathematical formulas needs arithemetic operations and inequality operations, so we extend the abstract syntax with the following:

\lstCode{Defining the mathematical operations.}{04_01_C04}
\begin{lstgf}
    cat
        Arg ; Math ; MathFormula ; ArithmeticOperation ; InequalityOperation ;

    fun
        -- Mathematic operations
        _MathArg : Arg -> Math ;
        _FinalFormula : Math -> InequalityOperation -> Math -> MathFormula ;
        _MathArgs : Math -> ArithmeticOperation -> Math -> Math ;

        _Division, _Addition, _Subtraction, _Multiplication : ArithmeticOperation ;
        _Greater, _GreaterEqual, _Equal, _LessEqual, _Less : InequalityOperation ;
\end{lstgf}

All mathematical formulas in the logical formulas have some sort of inequality operation. Therefore, \cf{\_FinalFormula} is the only \cf{MathFormula}, and it is made from a \cf{Math}, an \cf{InequalityOperation} and a second \cf{Math}.

\cf{\_Math} simply takes an \cf{Arg} and produces a \cf{Math}. This can be used either for the \cf{\_FinalFormula} directly, or for the \cf{\_MathArgs} that takes two \cf{Math}s and a \cf{ArithmeticOperation} and returns a \cf{Math}. That way, a \cf{\_MathArgs} can be used inside a \cf{\_MathArgs} to signify multiple operations.

The \cf{InequalityOperation} and \cf{ArithmeticOperation} represents the different inequality- and arithmetic op-erations and should require no explanation

\note{Some sort of ending}.