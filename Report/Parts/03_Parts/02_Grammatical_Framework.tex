\section{Grammatical Framework}
\label{03_02}

\note{Some sort of transition from linear logic to this section is needed here. End it with "throughout this section, we will construct a very simple program for interpreting LL."}

Grammatical Framework (GF) is an open-source multilingual programming language. With GF, one can write programs that can translate other languages. This works through parsing (analyzing a language), linearization (generating the language) and translation (analyzing one language to generate another one).

A GF program consists of an abstract module and one or more concrete modules. The abstract module defines what meanings can be interpreted and parsed by the grammar. The concrete module maps the abstract meanings to strings, thereby forming complete sentences.

The abstract module contains category declarations (\cf{cat}) and function declarations (\cf{fun}). The \cf{cat} list the different categories (meanings) used in the language, where the \cf{fun} dictates how the categories fit together to create meaning-building functions. The abstract syntax furthermore has a \cf{flag startcat} that indicates what category the program should start with. 

\lstCode{A simple abstract syntax.}{03_02_C01}
\begin{lstgf}
abstract AbstractLinearLogic = {
    flags startcat = Formula ;
    
    cat 
        Formula ; Connective ; Ident ;
    fun 
        _VoteCard, _BlankBallot : Ident ;
        _Lolli : Connective ;
        _Formula : Ident -> Connective -> Ident -> Formula;
}
\end{lstgf}

In the abstract syntax shown in \refCode{03_02_C01}, there are three categories: \cf{Formula}, \cf{Connective} and \cf{Ident}. Furthermore, there are three functions. The first function says that \cf{\_VoteCard} and \cf{\_BlankBallot} are of the type \cf{Ident}. The second function says that \cf{\_Lolli} is of the type \cf{Connective}. The last function says that \cf{\_Formula} takes three arguments (an \cf{Ident}, a \cf{Connective} and an \cf{Ident}) and returns something of the type \cf{Formula}.

The concrete module contains linearization type definitions (\cf{lincat}) and linearization definitions
(\cf{lin}). The \cf{lincat} determines the type of object for each category in the abstract syntax and the \cf{lin} determines what value is assigned to each abstract meaning. 

When the program parses a language, it will look for the values being held by the meanings and translate each into the abstract syntax. This abstract syntax forms an abstract syntax tree. The program can then turn the abstract syntax tree into any language supported by concrete implementations.

\lstCode{A concrete implementation of the abstract syntax from \refCode{03_02_C01} that understands linear logic.}{03_02_C02}
\begin{lstgf}
concrete ConcreteLinearLogic of AbstractLinearLogic = {
    lincat 
        Formula, Connective, Ident = {s = Str} ;
    lin 
        _VoteCard = {s = "voting-auth-card"} ;
        _BlankBallot = {s = "blank-ballot"} ;
        _Lolli = {s = "-o"} ;
        _Formula i1 c i2 = {s = i1.s ++ c.s ++ "{" ++ i2.s ++ "}"} ;
}
\end{lstgf}

In \refCode{03_02_C02} \cf{Formula}, \cf{Connective} and \cf{Ident} have been defined as records that can hold a \cf{Str} (a string). \cf{\_VoteCard}, \cf{\_BlankBallot } and \cf{\_Lolli} corresponds to a certain string. 
\cf{\_Formula} has a more advanced meaning, however, as it consists of the value of \cf{i1} concatenated with the value of \cf{c} and \cf{i2} inside curly brackets. \cf{i1}, \cf{c} and \cf{i2} are the arguments it takes according to the abstract syntax.

\lstCode{A concrete implementation of the abstract syntax from \refCode{03_02_C01} that understands English.}{03_02_C03}
\begin{lstgf}
concrete ConcreteEnglish of AbstractLinearLogic = {
    lincat 
        Formula, Connective, Ident = {s = Str} ;
    lin 
        _VoteCard = {s = "an authorization card"} ;
        _BlankBallot = {s = "a blank ballot"} ;
        _Lolli = {s = "then"} ;
        _Formula i1 c i2 = {s = "if i give" ++ i1.s ++ c.s ++ "i get" ++ i2.s} ;
}
\end{lstgf}

With another concrete implementation (such as the English one in \refCode{03_02_C03}), the program will be able to translate sentences from one language into the other, as long as the sentences adhere to the structure set by the abstract syntax. Or in this case, translate linear logic into English or the other way around.

Using the modules above, one would be able to translate "voting-auth-card -o \{ blank-ballot \}" into "if i give an authorization card then i get a blank ballot".

Some thing may happen a lot of times in a concrete implementation (such as concatenating two strings). GF can make this easier through operations (\cf{oper}), also known as functions in other programming languages. 

Operations can do two things. They can define a new type and they can be used with arguments to produce something. The latter type of operation consists of the following:
\begin{itemize}
\item \textbf{A name} that defines the \cf{oper} and is used when calling it. The operation in \refCode{03_02_C04} has the name "cc" (concatenate).

\item \textbf{Arguments, their types and the return type.} The operation in \refCode{03_02_C04} takes two arguments of the type Str called "x" and "y" and returns something of the type Str.

\item \textbf{The actual operation}. The operation in \refCode{03_02_C04} concatenates the two given strings and returns the result.
\end{itemize}

\lstCode{A simple operation in GF that concatenates two strings.}{03_02_C04}
\begin{lstgf}
oper 
    cc : Str -> Str -> Str = \x,y -> (x.s ++ y.s) ;
\end{lstgf}

An operation can be placed in a concrete implementation (if it is only needed in one of them) or in a so-called \cf{resource} module (see \refCode{03_02_C05}), which can be accessed by multiple concrete implementations. To access the resource module, one adds "open $<$nameOfModule$>$ in" to the first line of a concrete implementation, as shown in \ref{code:03_02_C06}

\lstCode{A simple resource module.}{03_02_C05}
\begin{lstgf}
resource StringOper = {
    oper
        SS : Type = {s : Str} ;
        ss : Str -> SS = \x -> {s = x} ;
        cc : SS -> SS -> SS = \x,y -> ss (x.s ++ y.s) ;
        prefix : Str -> SS -> SS = \p,x -> ss (p ++ x.s) ;
}
\end{lstgf}

\lstCode{Using the resource module.}{03_02_C06}
\begin{lstgf}
concrete HelloEng of AbstractLinearLogic = open StringOper in {
    lincat 
        Formula, Connective, Ident = SS ;
    lin 
        _VoteCard = ss ("voting-auth-card") ;
        _BlankBallot = ss ("blank-ballot") ;
        _Lolli = ss ("-o") ;
        _Formula i1 c i2 = ss (i1.s ++ c.s ++ "{" ++ i2.s ++ "}") ;
}
\end{lstgf}

Looking back at line 8 in \refCode{03_02_C03}, \cf{\_VoteCard} and \cf{\_BlankBallot} only exist in singular version. Suppose we want to be able to use the plural version of them, we could do two things: We could make a \cf{\_VoteCardSg} and a \cf{\_VoteCardPl} that holds the singular and plural version respectively. Or we could use parameters to choose.

A parameter consists of the parameter's name and its values, seperated by a horizonal line. Parameters can be added to either a resource module (by which the implementations using the resource module have access to it) or to a concrete implementation (limiting the parameter to that implementation).

\lstCode{Defining a parameter for singular and plural versions of a word.}{03_02_C07}
\begin{lstgf}
param 
    Number = Sg | Pl ;
\end{lstgf}

The \cf{lincat} of a type also needs to reflect the fact that it takes a parameter. This is done as shown in \ref{code:03_02_C07}.

\lstCode{Telling the \cf{lincat} that it takes a parameter and generates a Str.}{03_02_C08}
\begin{lstgf}
lincat 
    Recipient = {s : Number => Str} ;
\end{lstgf}

The last thing that needs to be changed is the \cf{lin} of the category that has been changed. Tables are used for this purpose. A table holds the different possibilities for the parameter, along with what is returned based on the parameter. \refCode{03_02_C08} shows a table for \_VoteCard.

\lstCode{A table for \_VoteCard that returns the singular or plural version of the word depending on the parameter.}{03_02_C09}
\begin{lstgf}
lin 
    _VoteCard = {
        s = table {
            Sg => "an authorization card" ;
            Pl => "multiple authorization cards"
        }
    } ;
\end{lstgf}

\refCode{03_02_C10} shows how the parameters are used in the ConcreteEnglish program. Assuming i1 is \cf{\_VoteCard}, and i2 is \cf{\_BlankBallot}, FormulaSg creates a string saying "if i give an authorization card then i get a blank ballot" where line 2 creates a string saying "if i give multiple authorization cards i get multiple blank ballots".

\lstCode{Giving the parameter as an argument.}{03_02_C10}
\begin{lstgf}
lin
    FormulaSg i1 c i2 = {s = "if i give" ++ i1.s ! Sg ++ c.s ++ "i get" ++ i2.s ! Sg} ;
    FormulaPl i1 c i2 = {s = "if i give" ++ i1.s ! Pl ++ c.s ++ "i get" ++ i2.s ! Pl} ;
\end{lstgf}

\note{Need some sort of ending here.}