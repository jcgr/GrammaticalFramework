\section{Grammatical Framework}
\label{03_02}

Grammatical Framework (GF) is an open-source multilingual programming language. With GF, one can write programs that can translate from one language into another. This works through parsing (analyzing a language), linearization (generating the language) and translation (analyzing one language to generate another one). In this section, I will go over the aspects of GF that have been used in writing the program.

A GF program consists of an abstract module and one or more concrete modules. The abstract module defines what meanings can be interpreted and parsed by the grammar. The concrete module maps the abstract meanings to strings, thereby forming complete sentences.

\subsection{The abstract module}
\label{03_02_01}

The abstract module contains category declarations (\cf{cat}) and function declarations (\cf{fun}). The \cf{cat} list the different categories (meanings) used in the language, where the \cf{fun} dictates how the categories fit together to create meaning-building functions. The abstract syntax furthermore has a \cf{flag startcat} that indicates what category the input should correspond to.

\lstCode{A simple abstract syntax.}{03_02_C01}
\begin{lstgf}
abstract AbstractLinearLogic = {
    flags startcat = Formula ;
    
    cat 
        Formula ; Connective ; Ident ;
    fun 
        _VoteCard, _BlankBallot : Ident ;
        _Lolli : Connective ;
        _Formula : Ident -> Connective -> Ident -> Formula;
}
\end{lstgf}

In the abstract syntax shown in \refCode{03_02_C01}, there are three categories: \cf{Formula}, \cf{Connective} and \cf{Ident}. Furthermore, there are three functions. The first function says that \cf{\_VoteCard} and \cf{\_BlankBallot} are of the type \cf{Ident}. The second function says that \cf{\_Lolli} is of the type \cf{Connective}. The last function says that \cf{\_Formula} takes three arguments (an \cf{Ident}, a \cf{Connective} and an \cf{Ident}) and returns something of the type \cf{Formula}.

\subsection{The concrete module}
\label{03_02_02}

The concrete module contains linearization type definitions (\cf{lincat}) and linearization definitions (\cf{lin}). The \cf{lincat} determines the type of object for each category in the abstract syntax and the \cf{lin} determines what value is assigned to each abstract meaning. 

When the program parses a language, it will look for the values held by the meanings and translate each into the abstract syntax. This abstract syntax forms an abstract syntax tree. The program can then turn the abstract syntax tree into any language supported by concrete implementations.

\lstCode{A concrete implementation of the abstract syntax from \refCode{03_02_C01} that understands linear logic.}{03_02_C02}
\begin{lstgf}
concrete ConcreteLinearLogic of AbstractLinearLogic = {
    lincat 
        Formula, Connective, Ident = {s = Str} ;
    lin 
        _VoteCard = {s = "voting-auth-card"} ;
        _BlankBallot = {s = "blank-ballot"} ;
        _Lolli = {s = "-o"} ;
        _Formula i1 c i2 = {s = i1.s ++ c.s ++ "{" ++ i2.s ++ "}"} ;
}
\end{lstgf}

In \refCode{03_02_C02} \cf{Formula}, \cf{Connective} and \cf{Ident} have been defined as records that can hold a \cf{Str} (a string). \cf{\_VoteCard}, \cf{\_BlankBallot } and \cf{\_Lolli} corresponds to a certain string. 
\cf{\_Formula} has a more advanced meaning, however, as it consists of the value of \cf{i1} concatenated with the value of \cf{c} and \cf{i2} inside curly brackets. \cf{i1}, \cf{c} and \cf{i2} are the arguments it takes according to the abstract syntax.

\lstCode{A concrete implementation of the abstract syntax from \refCode{03_02_C01} that understands English.}{03_02_C03}
\begin{lstgf}
concrete ConcreteEnglish of AbstractLinearLogic = {
    lincat 
        Formula, Connective, Ident = {s = Str} ;
    lin 
        _VoteCard = {s = "an authorization card"} ;
        _BlankBallot = {s = "a blank ballot"} ;
        _Lolli = {s = "then"} ;
        _Formula i1 c i2 = {s = "if i give" ++ i1.s ++ c.s ++ "i get" ++ i2.s} ;
}
\end{lstgf}

With another concrete implementation (such as the English one in \refCode{03_02_C03}), the program will be able to translate sentences from one language into the other, as long as the sentences adhere to the structure set by the abstract syntax. Or in this case, translate linear logic into English or the other way around.

Using the modules above, one would be able to translate "voting-auth-card -o \{ blank-ballot \}" into "if i give an authorization card then i get a blank ballot".

\subsection{Operations}
\label{03_02_03}

Some thing may happen a lot of times in a concrete implementation (such as concatenating two strings). GF can make this easier through operations (\cf{oper}), also known as functions in other programming languages. 

Operations can do two things. They can define a new type and they can be used with arguments to produce/modify something. The latter type of operation consists of the following:
\begin{itemize}
\item \textbf{A name} that defines the \cf{oper} and is used when calling it. The operation in \refCode{03_02_C04} has the name "cc" (concatenate).

\item \textbf{Arguments, their types and the return type.} The operation in \refCode{03_02_C04} takes two arguments of the type Str called "x" and "y" and returns something of the type Str.

\item \textbf{The actual operation}. The operation in \refCode{03_02_C04} concatenates the two given strings and returns the result.
\end{itemize}

\lstCode{A simple operation in GF that concatenates two strings.}{03_02_C04}
\begin{lstgf}
oper 
    cc : Str -> Str -> Str = \x,y -> (x.s ++ y.s) ;
\end{lstgf}

An operation can be placed in a concrete implementation (if it is only needed in one of them) or in a so-called \cf{resource} module (see \refCode{03_02_C05}), which can be accessed by multiple concrete implementations. To access the resource module, one adds "open $\langle$nameOfModule$\rangle$ in" to the first line of a concrete implementation, as shown in \ref{code:03_02_C06}. 

\lstCode{A simple resource module.}{03_02_C05}
\begin{lstgf}
resource StringOper = {
    oper
        SS : Type = {s : Str} ;
        ss : Str -> SS = \x -> {s = x} ;
        cc : SS -> SS -> SS = \x,y -> ss (x.s ++ y.s) ;
        prefix : Str -> SS -> SS = \p,x -> ss (p ++ x.s) ;
}
\end{lstgf}

\lstCode{Using the resource module.}{03_02_C06}
\begin{lstgf}
concrete HelloEng of AbstractLinearLogic = open StringOper in {
    lincat 
        Formula, Connective, Ident = SS ;
    lin 
        _VoteCard = ss ("voting-auth-card") ;
        _BlankBallot = ss ("blank-ballot") ;
        _Lolli = ss ("-o") ;
        _Formula i1 c i2 = ss (i1.s ++ c.s ++ "{" ++ i2.s ++ "}") ;
}
\end{lstgf}

One should note that GF comes with built-in resource modules. The module "Prelude" has the same operations as the StringOper module shown.

\subsection{Variable bindings}
\label{03_02_04}

The last thing we are going to look at is variable bindings. These are used to bind variables, which allows for them to be used dynamically in parts of the program. In the universal quantification \cf{( Pi x : nat ) ( x = x )}, the variable \cf{x} has a binding (\cf{( Pi x : nat )}) that says it is of the type \cf{nat} (a natural number), and that it is bound in the body \cf{(x = x )}, where it can be used.

To use variable bindings in GF, it is necessary to use functions as arguments. In \refCode{03_02_C07}, \cf{Pi} takes an argument \cf{A} that is required to be of the type \cf{Set}. It also takes an \cf{El A} argument, a type that takes a \cf{Set} as argument (in this case \cf{A}), and uses that in a \cf{Set}. These two groups of arguments are used to create a \cf{Set}. Furthermore, \cf{Eq} also takes an argument \cf{A} that is of the type \cf{Set}. It also takes two other arguments, \cf{a} and \cf{b}, which both must be of the type \cf{El} with \cf{A} as its argument. This is turned into a \cf{Set}.


\lstCode{Abstract syntax for variable bindings.}{03_02_C07}
\begin{lstgf}
    cat
        Set ;
        El Set ;
    fun
        Pi : (A : Set) -> (El A -> Set) -> Set ;
        Eq : (A : Set) -> (a,b : El A) -> Set ;
        Nat : Set ;
\end{lstgf}

The concrete syntax needs to use a special syntax for the variable bindings as well. Previously, we have used \cf{B.s} to return the string value of the argument. With variable bindings the syntax is expanded, as shown in \refCode{03_02_C08}, with the addition of \cf{B.\$0} while \cf{B.s} is still used.

\lstCode{The concrete syntax for variable bindings.}{03_02_C08}
\begin{lstgf}
    lincat
        Set, El = SS ;
    lin
        Pi A B = ss ( "(" ++ "Pi" ++ B.$0 ++ ":" ++ A.s ++ ")" ++ B.s ) ;
        Eq A a b = ss ( "(" ++ a.s ++ "=" ++ b.s ++ ")" ) ;
        Nat = ss ( "nat" ) ;
\end{lstgf}

A normal argument would have the type \cf{\{ s : Str \}}, but a function argument has the type \cf{\{ s : Str ; \$0 : Str \}}. It means that the argument \cf{B.\$0} is bound and can be used in \cf{B.s}. If the function had more arguments \cf{.\$1}, \cf{.\$2}, etc. would have been used to refer to them.

In \refCode{03_02_C08}, \cf{( Pi x : nat )} will bind the variable \cf{x} (\cf{B.\$0}) and allow the use of it in \cf{B.s}. \cf{B.s} could be \cf{Eq}, which would end up being \cf{( x = x )}. At this point, one might wonder why the \cf{A} argument is not used in \cf{Eq}. The \cf{A} argument shows the type of \cf{a} and \cf{b}, but as we already do that with \cf{Pi}, there is no reason to write it next to the arguments.

The use of variable bindings allows for a more fexible program, and is important when working with logical formulas where it is never certain how many variables are going to be used, and what they are going to represent.

This covers linear logic, Celf and Grammatical Framework and should give the reader a fair idea of how they work.